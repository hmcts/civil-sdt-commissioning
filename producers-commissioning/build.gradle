dependencies {
  implementation 'com.google.guava:guava:32.1.3-jre'

  testImplementation project(path: ':producers-commissioning')
}

sourceSets {
  testUnit {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/unit-test/java')
    }
    resources.srcDir file('src/unit-test/resources')
  }

  testE2E {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/end-to-end-test/java')
    }
    resources.srcDir file('src/end-to-end-test/resources')
  }
}

configurations {
  testUnitImplementation.extendsFrom testImplementation
  testUnitImplementation.extendsFrom runtimeOnly

  testE2EImplementation.extendsFrom testImplementation
  testE2ERuntimeOnly.extendsFrom runtimeOnly
}

tasks.withType(Test).configureEach {
  useJUnitPlatform()
}

pitest {

  junit5PluginVersion = '1.1.2'
  targetClasses.set([
          'uk.gov.moj.sdt.producers.comx.cache.*',
          'uk.gov.moj.sdt.producers.comx.dao.*',
          'uk.gov.moj.sdt.producers.comx.sdtws.*',
          'uk.gov.moj.sdt.producers.comx.services.*',
          'uk.gov.moj.sdt.producers.comx.utils.*'
  ])
  targetTests.set(['uk.gov.moj.sdt.producers.comx.cache.*',
                   'uk.gov.moj.sdt.producers.comx.dao.*',
                   'uk.gov.moj.sdt.producers.comx.sdtws.*',
                   'uk.gov.moj.sdt.producers.comx.services.*',
                   'uk.gov.moj.sdt.producers.comx.utils.*'])
  testSourceSets.set([sourceSets.testUnit, sourceSets.testE2E])
  mainSourceSets.set([sourceSets.main])
  useClasspathFile = true
  enableDefaultIncrementalAnalysis = true
  historyInputLocation = 'build/reports/pitest/fastermutationtesting'
  historyOutputLocation = 'build/reports/pitest/fastermutationtestingoutput'
  threads = 15
  fileExtensionsToFilter.addAll('XML','JSON')
  outputFormats.set(['XML', 'HTML','CSV'])
  mutationThreshold = 0
  coverageThreshold = 0
  jvmArgs.set(['-Xms1G','-Xmx3G'])
  timestampedReports = true
  failWhenNoMutations = false
  detectInlinedCode = true
}

test {
  useJUnitPlatform()

  testLogging {
    events "passed", "skipped", "failed"
  }

  reports {
    html.getRequired().set(true)
  }
  failFast = true

  description = "Runs Tests"
  testClassesDirs = sourceSets.testUnit.output.classesDirs
  classpath = sourceSets.testUnit.runtimeClasspath
}
