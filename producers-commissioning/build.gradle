dependencies {
  testImplementation project(path: ':producers-commissioning')
}

sourceSets {
  testUnit {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/unit-test/java')
    }
    resources.srcDir file('src/unit-test/resources')
  }

  testE2E {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/end-to-end-test/java')
    }
    resources.srcDir file('src/end-to-end-test/resources')
  }
}

configurations {
  testUnitImplementation.extendsFrom testImplementation
  testUnitImplementation.extendsFrom runtimeOnly

  testE2EImplementation.extendsFrom testImplementation
  testE2ERuntimeOnly.extendsFrom runtimeOnly
}

tasks.register('printSourceSetInformation') {
  doLast{
    sourceSets.each { srcSet ->
      println "["+srcSet.name+"]"
      print "-->Source directories: "+srcSet.allJava.srcDirs+"\n"
      print "-->Output directories: "+srcSet.output.classesDirs.files+"\n"
      print "-->Compile classpath:\n"
      srcSet.compileClasspath.files.each {
        print "  "+it.path+"\n"
      }
      println ""
    }
  }
}

tasks.withType(Test).configureEach {
  useJUnitPlatform()
}

pitest {
  junit5PluginVersion = '1.1.2'
  targetClasses.set(['uk.gov.moj.sdt.producers.comx.cache.*',
                   'uk.gov.moj.sdt.producers.comx.dao.*',
                   'uk.gov.moj.sdt.producers.comx.sdtws.*',
                   'uk.gov.moj.sdt.producers.comx.services.*',
                   'uk.gov.moj.sdt.producers.comx.utils.*'])
  targetTests.set(['uk.gov.moj.sdt.producers.comx.cache.*',
                 'uk.gov.moj.sdt.producers.comx.sdtws.*'])
  enableDefaultIncrementalAnalysis = true
  historyInputLocation = 'build/reports/pitest/fastermutationtesting'
  historyOutputLocation = 'build/reports/pitest/fastermutationtestingoutput'
  threads = 15
  testSourceSets.set([sourceSets.testUnit, sourceSets.testE2E])
  mainSourceSets.set([sourceSets.main])
  fileExtensionsToFilter.addAll('xml','json')
  outputFormats.set(['XML', 'HTML','CSV'])
  mutationThreshold = 0
  coverageThreshold = 0
  jvmArgs.set(['-Xms1G','-Xmx3G'])
  timestampedReports = true
  failWhenNoMutations = false
  detectInlinedCode = true
}
